#include <Servo.h>

#include "irsharp.h"
#include "communication.h"
#include "Hdlc.h"
#include "mcdriver.h"

// comment out for USB serial
#define USE_XBEE_FOR_TELEMETRY

#ifdef USE_XBEE_FOR_TELEMETRY
	#define SERIALDEV Serial3
#else
	#define SERIALDEV Serial
#endif

#define TEENSY_LED          13
#define IR_LEFT             A9
#define IR_RIGHT            A8
#define IR_FRONT_LEFT       A7
#define IR_FRONT_RIGHT      A6
#define IR_FRONT            A5

#define STEERING_PWM_PIN    4
#define DRIVE_PWM_PIN       3

bc_telemetry_packet_t telemetry;

IRSharp sharp_left(IR_LEFT);
IRSharp sharp_right(IR_RIGHT);
IRSharp sharp_front_left(IR_FRONT_LEFT);
IRSharp sharp_front_right(IR_FRONT_RIGHT);
IRSharp sharp_front(IR_FRONT);

uint8_t m_rx_buffer[255];
uint8_t m_rx_len = 0;
uint8_t m_tx_buffer[255];
uint8_t m_tx_len = 0;
HDLC hdlc(m_rx_buffer, 255);

Servo steeringservo;
Servo drivingservo;

bool m_automatic = false;

uint32_t last_time = 0;

MCDriver driver;

void send_telemetry() {
	m_tx_len = hdlc.encode((uint8_t*) &telemetry, sizeof(bc_telemetry_packet_t),
			m_tx_buffer);
	SERIALDEV.write(m_tx_buffer, m_tx_len);
}

void setup() {
	SERIALDEV.begin(57600);

	steeringservo.attach(STEERING_PWM_PIN);
	drivingservo.attach(DRIVE_PWM_PIN);

	analogReference(DEFAULT);
	analogReadAveraging(16);
	analogReadResolution(10);
	pinMode(TEENSY_LED, OUTPUT);

	telemetry.header = BC_TELEMETRY;
}

void toggle_led() {
	static bool ledon = true;
	if (ledon) {
		ledon = false;
		digitalWrite(TEENSY_LED, HIGH);
	} else {
		ledon = true;
		digitalWrite(TEENSY_LED, LOW);
	}
}

void loop() {
	while (SERIALDEV.available() > 0) {
		m_rx_len = hdlc.decode(SERIALDEV.read());

		// check if HDLC packet is received
		if (m_rx_len > 0) {
			uint8_t header = ((uint8_t*) m_rx_buffer)[0];

			if (CB_MOTOR_COMMAND == header) {
				cb_motor_command_packet_t* motor =
						(cb_motor_command_packet_t*) m_rx_buffer;
				m_automatic = motor->automatic;
				if (m_automatic) {
					driver.set_drive_pwm(motor->drive_pwm);
				}
				else {
					steeringservo.write(motor->steering_pwm);
					drivingservo.write(motor->drive_pwm);
				}
			}
		}
	}

	telemetry.time = millis();
	// do not use left/right sensor
	telemetry.ir_left = 10;
	telemetry.ir_right = 10;
	telemetry.ir_front_left = sharp_front_left.distance();
	telemetry.ir_front_right = sharp_front_right.distance();
	telemetry.ir_front = sharp_front.distance();

	drive_cmd_t& driveCmd = driver.drive(telemetry);
	if (m_automatic) {
		steeringservo.write(driveCmd.steeringPwm);
		drivingservo.write(driveCmd.drivingPwm);
		toggle_led();
	}

	if (telemetry.time > last_time + 20) {
		send_telemetry();
		last_time = telemetry.time;
	}

	//Serial.print("left: ");
	//Serial.println(m_distance_left);
}

